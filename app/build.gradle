apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    def versionPropsFile = rootProject.file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))

        def majorVersionProperty = versionProps['MAJOR_VERSION'].toInteger()
        def minorVersionProperty = versionProps['MINOR_VERSION'].toInteger()
        def versionCodeProperty = versionProps['VERSION_CODE'].toInteger()
        def updatedBuildVersion = versionProps['BUILD_VERSION'].toInteger() + 1

        versionProps['BUILD_VERSION']=updatedBuildVersion.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        def updatedVersionName = majorVersionProperty + "." + minorVersionProperty + "." + versionCodeProperty + "." + updatedBuildVersion

        defaultConfig {
            applicationId "net.frostedbytes.android.trendo"
            minSdkVersion 24
            targetSdkVersion 28
            versionCode versionCodeProperty
            versionName updatedVersionName
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.applicationId}-${variant.name}-${variant.versionName}.apk"
        }
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField 'Boolean', 'enableCrashlytics', 'true'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'enableCrashlytics', 'false'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    allprojects {
        repositories {
            maven {
                url "https://jitpack.io"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // Google
    implementation 'com.google.android.gms:play-services-auth:17.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    // Firebase
    implementation 'com.google.firebase:firebase-core:17.2.1'
    implementation 'com.google.firebase:firebase-database:19.2.0'
    implementation 'com.google.firebase:firebase-auth:19.2.0'
    implementation 'com.google.firebase:firebase-config:19.0.3'
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    // Firebase UI
    implementation('com.firebaseui:firebase-ui-database:3.1.0') {
        exclude module:'runtime'
        exclude module:'extensions'
    }
    // Room
    implementation "androidx.room:room-runtime:2.2.2"
    annotationProcessor "androidx.room:room-compiler:2.2.2"
    androidTestImplementation "androidx.room:room-testing:2.2.2"
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    // Testing dependencies
    testImplementation 'org.robolectric:robolectric:4.2.1'
    testImplementation 'com.squareup.assertj:assertj-android:1.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.code.gson:gson:2.8.5'
    testImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Charting library
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
}

apply plugin: 'com.google.gms.google-services'
